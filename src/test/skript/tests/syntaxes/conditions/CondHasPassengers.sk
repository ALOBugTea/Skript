test "has passengers":
	spawn a cow at spawn of world "world"
	set {_cow} to last spawned cow
	spawn a pig at spawn of world "world":
		assert event-entity has no passengers with "The pig had passengers after being spawned"
		set passengers of event-entity to {_cow}
		assert event-entity has a passenger with "The pig did not have any passengers"
		assert size of passengers is 1 with "The size of the passengers was incorrect"
		eject any passengers from event-entity
		make event-entity ride on {_cow}
		assert {_cow} has a passenger with "The cow did not have any passengers"
		assert vehicle of {_cow} is event-entity with "The cow did not have the pig as a passenger"
		clear event-entity
		assert event-entity is dead with "The pig is not dead"
	eject any passengers from {_cow} # takes a tick for the game to register the pig died to update passengers
	assert vehicle of {_cow} is not set with "The cow had passengers 1"
	assert {_cow} does not have a passenger with "The cow had passengers 2"
	make {_cow} ride a sheep # Spawns a sheep
	assert last spawned sheep is set with "A sheep did not get set"
	assert passengers of last spawned sheep contains {_cow} with "The sheep did not have the cow as a passenger"
	eject any passengers from the last spawned sheep
	assert passengers of the last spawned sheep is not set with "The last spawned sheep had passengers"
	clear all entities
	clear {_cow}
